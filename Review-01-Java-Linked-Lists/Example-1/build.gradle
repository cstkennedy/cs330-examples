buildscript {
    repositories {
        mavenCentral()
    }
}

plugins {
    id "java"
    id "application"
    id "eclipse"

    id "checkstyle"
    id "com.github.spotbugs" version "5.0.9"
    id "project-report"
    id "jacoco"
    id "pmd"
}

java {
    sourceCompatibility = JavaVersion.toVersion(11)
    targetCompatibility = JavaVersion.toVersion(11)
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter:5.8.2'
    // testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testImplementation 'org.hamcrest:hamcrest-library:2.2'
}

jar {
    archiveBaseName = "ListDriver"

    manifest {
        attributes(
            "Main-Class": "ListDriver"
        )
    }
}

run {
    mainClass = "ListDriver"
}

test {
    useJUnitPlatform()
    reports {
        html.required = true
    }
    ignoreFailures = true

    testLogging {
        events "passed", "skipped", "failed", "standardOut", "standardError"
    }

    // finalizedBy jacocoTestReport
}

javadoc {
    failOnError false
}

task deploy(dependsOn: [jar, javadoc, check]) {

}

jacoco {
    toolVersion = "0.8.11"
}

jacocoTestReport {
    reports {
        html.required = true
        xml.required = true
        csv.required = true
    }
}

//------------------------------------------------------------------------------
// Analysis Tools
//------------------------------------------------------------------------------
spotbugsMain {
    ignoreFailures = true
    effort = "max"
    reportLevel = "medium"
    reports {
       xml.enabled = false
       html.enabled = true
    }
}

spotbugsTest.enabled = false

checkstyle {
    ignoreFailures = true
    showViolations = false
}

tasks.withType(Checkstyle) {
    reports {
        html.destination project.file("build/reports/checkstyle/main.html")
    }
}
checkstyleTest.enabled = false

pmd {
    ignoreFailures = true
    ruleSets = [
        "category/java/bestpractices.xml",
        "category/java/codestyle.xml",
        "category/java/design.xml",
        "category/java/errorprone.xml",
        "category/java/performance.xml"
    ]
}
