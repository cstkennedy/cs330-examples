buildscript {
    repositories {
        jcenter()

        // Add SpotBugs required repo
        maven {
            url "https://plugins.gradle.org/m2/"
        }
    }

    dependencies {
        classpath "org.ysb33r.gradle:doxygen:0.2"

        // Add SpotBugs
        classpath "gradle.plugin.com.github.spotbugs:gradlePlugin:1.6.0"
    }
}

apply plugin: "java"
apply plugin: "application"
apply plugin: "eclipse"
apply plugin: "org.ysb33r.doxygen"
apply plugin: "jacoco"

apply plugin: "findbugs"
apply plugin: "pmd"
apply plugin: "com.github.spotbugs"

apply plugin: "checkstyle"


sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
    testCompile "junit:junit:4.12"
    testCompile "org.hamcrest:hamcrest-library:1.3"

}

jar {
    baseName = "RunShapes"

    manifest {
        attributes(
            "Main-Class": "edu.odu.cs.cs330.examples.RunShapes"
        )
    }
}

run {
    main = "edu.odu.cs.cs330.examples.RunShapes"
    args = ["src/main/resources/inputShapes.txt", "2"]
}

test {
    reports {
        html.enabled = true
    }
}

javadoc {
    failOnError false
}

doxygen {
    generate_html true

    template "documentation.config"

    source new File(projectDir, "src/main/java")
    //source new File(projectDir, "src/test/java")
    outputDir new File(buildDir, "docs")
}

/**
 * Run code style/error checks
 */
tasks.withType(FindBugs) {
    ignoreFailures true
    reports {
        xml.enabled false
        html.enabled true
    }
}

pmd {
    ignoreFailures = true
    ruleSets = [
        "java-basic",
        "java-braces",
        "java-clone",
        "java-codesize",
        //"java-comments",
        //"java-controversial",
        //"java-coupling",
        //"java-design",
        "java-empty",
        //"java-finalizers",
        "java-imports",
        "java-junit",
        "java-naming",
        //"java-optimizations",
        "java-strictexception",
        "java-strings",
        "java-sunsecure",
        "java-typeresolution",
        "java-unnecessary",
        "java-unusedcode"
    ]
}

// SpotBugs
spotbugsMain {
    ignoreFailures = true
    effort = "max"
    reportLevel = "medium"
    reports {
       xml.enabled = false
       html.enabled = true
    }
}

spotbugsTest.enabled = false
// End SpotBugs config

jacoco {
    toolVersion = "0.8.0"
}

jacocoTestReport {
    reports {
        html.enabled true
        xml.enabled true
        csv.enabled true
    }
}


// Check Style Config
checkstyle {
    ignoreFailures = true
    showViolations = false
}

tasks.withType(Checkstyle) {
    reports {
        html.destination project.file("build/reports/checkstyle/main.html")
    }
}
checkstyleTest.enabled = false
// End Checkstyle config
